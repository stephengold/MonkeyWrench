// Gradle script to build the MonkeyWrench project

plugins {
    id 'base' // to add a "clean" task to the root project
    alias(libs.plugins.download) // to retrieve files from URLs
}

ext {
    //wrenchSnapshot = '-SNAPSHOT' // for development builds
    wrenchSnapshot = '' // for release builds

    jmeTarget = '' // distinguish non-JME libraries built for specific JME releases

    wrenchVersion = '1.0.0' + jmeTarget
}

subprojects {
    apply from: rootProject.file('common.gradle')
}

configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // to disable caching of snapshots
}

tasks.register('checkstyle') {
    dependsOn ':apps:checkstyleMain', ':lib:checkstyleMain'
    description = 'Checks the style of all Java sourcecode.'
}

// Register publishing tasks:

tasks.register('install') {
    dependsOn ':lib:install'
    description = 'Installs Maven artifacts to the local repository.'
}
tasks.register('release') {
    dependsOn ':lib:release'
    description = 'Stages Maven artifacts to Sonatype OSSRH.'
}

// Register cleanup tasks:

tasks.register('cleanDownloads', Delete) { // downloaded assets
    delete 'downloads'
}

// Register downloading tasks:

tasks.register('bistro', Download) {
    src 'https://developer.nvidia.com/bistro'
    dest file('downloads/Bistro_v5_2.zip')
    overwrite false
}
tasks.register('downloads') {
    dependsOn 'testdata-31', 'testdata-36'
}
tasks.register('testdata-31', Download) {
    src 'https://repo1.maven.org/maven2/org/jmonkeyengine/jme3-testdata/3.1.0-stable/jme3-testdata-3.1.0-stable.jar'
    dest file('downloads/jme3-testdata-3.1.0-stable.jar')
    overwrite false
}
tasks.register('testdata-36', Download) {
    src 'https://repo1.maven.org/maven2/org/jmonkeyengine/jme3-testdata/3.6.1-stable/jme3-testdata-3.6.1-stable.jar'
    dest file('downloads/jme3-testdata-3.6.1-stable.jar')
    overwrite false
}
