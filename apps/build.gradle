// Gradle script to build the "apps" subproject of MonkeyWrench

// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

plugins {
    id 'application' // to build JVM applications
}

// Register tasks to run specific applications:

tasks.register('CompareLoaders', JavaExec) {
    dependsOn(':downloads')
    description 'Runs the CompareLoaders app.'
    mainClass = 'com.github.stephengold.wrench.test.CompareLoaders'
}

tasks.register('ImportMixamo', JavaExec) {
    description 'Runs the ImportMixamo app.'
    mainClass = 'com.github.stephengold.wrench.example.ImportMixamo'
}

tasks.register('TestIssue5232', JavaExec) {
    description 'Runs the test for issue 5232.'
    mainClass = 'com.github.stephengold.wrench.test.issue.TestIssue5232'
}
tasks.register('TestIssue5253', JavaExec) {
    description 'Runs the test for issue 5253.'
    mainClass = 'com.github.stephengold.wrench.test.issue.TestIssue5253'
}
tasks.register('TestIssue5289', JavaExec) {
    description 'Runs the test for issue 5289.'
    mainClass = 'com.github.stephengold.wrench.test.issue.TestIssue5289'
}
tasks.register('TestIssue5303', JavaExec) {
    description 'Runs the test for issue 5303.'
    mainClass = 'com.github.stephengold.wrench.test.issue.TestIssue5303'
}

application {
    mainClass = 'com.github.stephengold.wrench.test.CompareLoaders'
}
if (!hasProperty('mainClass')) {
    ext.mainClass = application.mainClass
}
jar.manifest.attributes('Main-Class': application.mainClass)

dependencies {
    implementation(libs.acorus)
    implementation(libs.gson)
    implementation(libs.heart)
    implementation(libs.wes)

    implementation(libs.jme3.plugins)
    implementation(libs.jme3.blender)
    runtimeOnly(libs.jme3.desktop)
    runtimeOnly(libs.jme3.lwjgl3)

    // MonkeyWrench doesn't use jme3-jogg
    //  -- it is included solely to avoid warnings from AssetConfig.
    runtimeOnly(libs.jme3.jogg)

    //implementation 'com.github.stephengold:MonkeyWrench:' + wrenchVersion // for the published library
    implementation project(':lib') // for latest sourcecode
}

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform
OperatingSystem os = DefaultNativePlatform.currentOperatingSystem
Boolean isMacOS = os.isMacOsX()

tasks.withType(JavaExec).configureEach { // Java runtime options:
    if (isMacOS) {
        jvmArgs '-XstartOnFirstThread' // required for GLFW on macOS
    }
    //args '--verbose' // to enable additional log output
    classpath sourceSets.main.runtimeClasspath
    dependsOn ':lib:assemble'
    enableAssertions true
    jvmArgs '-Xms2g', '-Xmx2g' // to enlarge the JVM heap
    jvmArgs '-XX:MaxDirectMemorySize=1200m' // to limit memory consumption by direct buffers
}

// Register cleanup tasks:

clean.dependsOn('cleanSandbox')

tasks.register('cleanSandbox', Delete) { // Acorus sandbox
    delete 'Written Assets'
}
